apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'org.jetbrains.dokka-android'
apply plugin: 'maven-publish'
apply plugin: 'signing'

def artifactId = 'android-sdk'
def groupId = 'com.payline'

android {

    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 29
        versionCode 1
        versionName "0.9.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    dokka {
        outputFormat = 'javadoc'
//        outputFormat = 'html-as-java'
        outputDirectory = "$rootDir/Documentation/javadoc"
        includeNonPublic = false
    }

}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.1.0'
    implementation 'com.google.android.material:material:1.0.0'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    testImplementation 'androidx.test:runner:1.2.0'
    testImplementation 'androidx.test.ext:junit:1.1.1'
    testImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    testImplementation 'org.robolectric:robolectric:4.3'

    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.ext:truth:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-web:3.2.0'
    androidTestImplementation 'org.awaitility:awaitility-kotlin:3.1.6'
    androidTestImplementation project(":tokenfetcher")
}

project.afterEvaluate {
    publishing {
        publications {
            library(MavenPublication) {
                setGroupId(groupId)
                setArtifactId(artifactId)
                version android.defaultConfig.versionName
                artifact(bundleReleaseAar)

                // maven-publish doesn't pick up on android library dependencies so we are manually adding them here
                pom.withXml() {
                    def dependenciesNode = asNode().appendNode('dependencies')

                    configurations.implementation.allDependencies.each {
                        def dependencyNode = dependenciesNode.appendNode("dependency")
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
                pom {
                    name = 'Payline Android SDK'
                    description = "This SDK features Payline's Mobile SDK, which allows a merchant's mobile application to use Payline services to realize an online payment or manage the wallet."
                    url = 'https://github.com/PaylineByMonext/payline-android-sdk'

                    licenses {
                        license {
                            name = 'GNU Lesser General Public License (LGPLv3)'
                            url = 'http://www.gnu.org/copyleft/lesser.html'
                        }
                    }
                    developers {
                        developer {
                            id = 'payline_dev'
                            name = 'Payline Dev Team'
                            email = 'support@payline.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:ssh://git@github.com:PaylineByMonext/payline-android-sdk.git'
                        developerConnection = 'scm:git:ssh://git@github.com:PaylineByMonext/payline-android-sdk.git'
                        url = 'https://github.com/PaylineByMonext/payline-java-sdk'
                    }
                }
            }
        }
        repositories {
            maven {
                def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                credentials { username ossrhUser; password ossrhPassword}
            }
        }
    }
    signing {
        sign publishing.publications.library
    }
}


